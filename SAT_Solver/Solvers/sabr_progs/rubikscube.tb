# SABR
# solve 3x3 rubiks cube

Sym{ b o w r g y }

Board{
.....		b1 b2 b3;
.....		b4 .  b6;
.....		b7 b8 b9;
r1 r2 r3	y1 y2 y3 	o1 o2 o3	w1 w2 w3;
r4 .  r6	y4 .  y6 	o4 .  o6	w4 .  w6;
r7 r8 r9	y7 y8 y9 	o7 o8 o9	w7 w8 w9;
.....		g1 g2 g3;
.....		g4 .  g6;
.....		g7 g8 g9;
}

Start{
		y g b;
		g	g;
		o b w;
r o g	w r b	o y y	o r b;
b	w	o	b	y	b	o	w;
b w w	b y g	y r g	r o r;
		r r o;
		g	w;
		w y y;
}

End{
		b b b;
		b   b;
		b b b;
r r r	y y y 		o o o	w w w;
r   r	y   y 		o   o	w	w;
r r r	y y y 		o o o	w w w;
		g g g;
		g   g;
		g g g;
}

Trans Clock:Side{

	t1 t2 t3;

l3	f1 f2 f3	r1;
l2	f4    f6	r2;
l1	f7 f8 f9	r3;

	b1 b2 b3;
=>
	l1 l2 l3;

b1	f7 f4 f1	t1;
b2	f8    f2	t2;
b3	f9 f6 f3	t3;

	r3 r2 r1;
}

Trans CounterClock:Side{

	t1 t2 t3;

l3	f1 f2 f3	r1;
l2	f4    f6	r2;
l1	f7 f8 f9	r3;

	b1 b2 b3;
=>
	r1 r2 r3;

t3	f3 f6 f9	b3;
t2	f2    f8	b2;
t1	f1 f4 f7	b1;

	l3 l2 l1;
}

DesObj Front:Side{
	b7 b8 b9;
	
r3	y1 y2 y3	o1;
r6	y4    y6	o4;
r9	y7 y8 y9	o7;
	
	g1 g2 g3;
}

DesObj Top:Side{
	w3 w2 w1;

r1	b1 b2 b3	o3;
r2	b4    b6	o2;
r3	b7 b8 b9	o1;

	y1 y2 y3;
}

DesObj Back:Side{
	b3 b2 b1;

o3	w1 w2 w3	r1;
o6	w4    w6	r4;
o9	w7 w8 w9	r6;

	g8 g7 g6;
}

DesObj Bottom:Side{
	y7 y8 y9;

r9	g1 g2 g3	o7;
r8	g4    g6	o8;
r7	g7 g8 g9	o9;

	w9 w8 w7;
}

DesObj Left:Side{
	b1 b4 b7;

w3	r1 r2 r3	y1;
w6	r4    r6	y4;
w9	r7 r8 r9	y7;

	g7 g4 g1;
}

DesObj Right:Side{
	b9 b6 b3;

y3	o1 o2 o3	w1;
y6	o4    o6	w4;
y9	o7 o8 o9	w7;

	g3 g6 g9;
}